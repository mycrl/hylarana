plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.21'
    id 'com.github.willir.rust.cargo-ndk-android' version '0.3.4'
    id 'maven-publish'
}

apply plugin: 'com.github.willir.rust.cargo-ndk-android'
apply plugin: 'maven-publish'

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.ensarsarajcic.kotlinx:serialization-msgpack:0.5.5'
}

android {
    namespace 'com.github.mycrl'
    compileSdk 34

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    defaultConfig {
        minSdk 29
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a'
        }

        consumerProguardFiles 'consumer-rules.pro'
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_shared'
                cppFlags ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
}
cargoNdk {
    buildTypes {
        release {
            buildType = "release"
        }
        debug {
            buildType = "debug"
        }
    }

    apiLevel = 29
    targets = ["arm64"]
    module = "../../ffi"
    targetDirectory = "../target"
    librariesNames = ["libmirror.so"]
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/mycrl/mirror'
            credentials {
                username = System.getenv('GITHUB_USERNAME')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }

    publications {
        release(MavenPublication) {
            groupId = 'com.github.mycrl'
            artifactId = 'mirror'
            version = '0.5.1'

            afterEvaluate {
                from components.release
            }
        }
    }
}
