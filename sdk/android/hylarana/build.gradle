plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.0' apply true
    id "org.mozilla.rust-android-gradle.rust-android" version "0.9.6" apply true
    id 'maven-publish' apply true
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'
}

android {
    namespace 'com.github.mycrl'
    compileSdk 34

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    defaultConfig {
        minSdk 29
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a'
        }

        consumerProguardFiles 'consumer-rules.pro'
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_shared'
                cppFlags ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
}

cargo {
    profile = 'release'
    apiLevel = 29
    prebuiltToolchains = true
    pythonCommand = 'python3'
    module = './src/main/ffi'
    libname = 'hylarana'
    targets = ['arm64']
    targetDirectory = '../../../target'
    targetIncludes = ['libhylarana.so']
    exec { spec, toolchain ->
        def absolutePath = project.file("../../../target").absolutePath
        spec.environment("CARGO_TARGET_DIR", "$absolutePath")
    }
}

tasks.whenTaskAdded { task ->
    if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
        task.dependsOn 'cargoBuild'
    }
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/mycrl/hylarana'
            credentials {
                username = System.getenv('GITHUB_USERNAME')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }

    publications {
        release(MavenPublication) {
            groupId = 'com.github.mycrl'
            artifactId = 'hylarana'
            version = '0.3.0'

            afterEvaluate {
                from components.release
            }
        }
    }
}
