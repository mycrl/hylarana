plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    //noinspection GradleDependency
    id "org.jetbrains.kotlin.plugin.serialization" version "1.9.0" apply true
    id "org.mozilla.rust-android-gradle.rust-android" version "0.9.6" apply true
    id "maven-publish" apply true
}

dependencies {
    //noinspection UseTomlInstead,GradleDependency
    implementation "androidx.core:core-ktx:1.12.0"
    //noinspection UseTomlInstead,GradleDependency
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2"
}

android {
    namespace "com.github.mycrl"
    compileSdk 35

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

    defaultConfig {
        minSdk 29
        targetSdk 35

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters "arm64-v8a"
        }

        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
                cppFlags ""
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
        }

        debug {
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    externalNativeBuild {
        cmake {
            path file("src/main/cpp/CMakeLists.txt")
            version "3.22.1"
        }
    }
}

def nativeModulePath = project.file("src/main/rust").absolutePath
def cargoTargetPath = project.file("../target").absolutePath
def targetApiLevel = android.defaultConfig.targetSdkVersion.apiLevel
def ndkPath = android.ndkDirectory.absolutePath

cargo {
    profile = "release"
    apiLevel = targetApiLevel
    prebuiltToolchains = true
    pythonCommand = "python3"
    module = nativeModulePath
    libname = "android-native"
    targets = ["arm64"]
    targetDirectory = cargoTargetPath
    targetIncludes = ["libhylarana.so"]
    exec { spec, toolchain ->
        spec.environment("ANDROID_API_LEVEL", "$targetApiLevel")
        spec.environment("ANDROID_NDK_PATH", ndkPath)
    }
}

tasks.configureEach { task ->
    if ((task.name == "javaPreCompileDebug" || task.name == "javaPreCompileRelease")) {
        task.dependsOn "cargoBuild"
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/mycrl/hylarana"
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        release(MavenPublication) {
            groupId = "com.github.mycrl"
            artifactId = "hylarana"
            version = "0.4.0"

            afterEvaluate {
                from components.release
            }
        }
    }
}
